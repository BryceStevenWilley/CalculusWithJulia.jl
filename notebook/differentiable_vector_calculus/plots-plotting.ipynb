{
  "cells": [
    {
      "cell_type": "markdown",
      "source": [
        "# 2D and 3D plots in Julia with Plots"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using CalculusWithJulia\nusing CalculusWithJulia.WeaveSupport\nusing Plots\nnothing"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "This covers plotting the typical 2D and 3D plots in Julia with the `Plots` package.\n\n\n```\nusing Plots\nusing LinearAlgebra, ForwardDiff\nimport PyPlot\nimport Contour: contours, levels, level, lines, coordinates\n```\n\nWe will make use of some helper functions that will simplify plotting. These will be described in more detail in the following:\n\n```\nxs_ys(vs) = Tuple(eltype(vs[1])[vs[i][j] for i in 1:length(vs)] for j in eachindex(first(vs)))\nxs_ys(v,vs...) = xs_ys([v, vs...])\nxs_ys(r::Function, a, b, n=100) = xs_ys(r.(range(a, stop=b, length=n)))\n\nfunction arrow!(p, v; kwargs...)\n  if length(p) == 2\n     quiver!(xs_ys([p])..., quiver=Tuple(xs_ys([v])); kwargs...)\n  elseif length(p) == 3\n    # 3d quiver needs support\n    # https://github.com/JuliaPlots/Plots.jl/issues/319#issue-159652535\n    # headless arrow instead\n    plot!(xs_ys(p, p+v)...; kwargs...)\n\tend\nend\n```\n\nWe will also use the `ForwardDiff` for derivatives and use the \"prime\" notation:\n\n```\nusing ForwardDiff\nfunction D(f, n::Int=1)\n    n < 0 && throw(ArgumentError(\"n is a non-negative integer\"))\n    n == 0 && return f\n    n == 1 && return t -> ForwardDiff.derivative(f, float(t))\n    D(D(f), n-1)\nend\nBase.adjoint(r::Function) = D(r)\n```\n\nWe will need to manipulate contours directly, so pull in the `Contours` package, using `import` to avoid name collisions and explicitly listing the methods we will use:\n\n```\nimport Contour: contours, levels, lines, coordinates\n```\n\nFinally, we need some features for vectors:\n\n```\nusing LinearAlgebra\n```\n\n## Parametrically described curves in space\n\nLet $r(t)$ be a vector-valued function with values in $R^d$, $d$ being $2$ or $3$. A familiar example is the equation for a line that travels in the direction of $\\vec{v}$ and goes through the point $P$: $r(t) = P + t \\cdot \\vec{v}$.\nA *parametric plot* over $[a,b]$ is the collection of all points $r(t)$ for $a \\leq t \\leq b$.\n\n\nIn `Plots`, parameterized curves can be plotted through two interfaces, here illustrated for $d=2$: `plot(f1, f2, a, b)` or `plot(xs, ys)`. The former is convenient for some cases, but typically we will have a function `r(t)` which is vector-valued, as opposed to a vector of functions. As such, we only discuss the latter.\n\nAn example helps illustrate. Suppose $r(t) = \\langle \\sin(t), 2\\cos(t) \\rangle$ and the goal is to plot the full ellipse by plotting over $0 \\leq t \\leq 2\\pi$. As with plotting of curves, the goal would be to take many points between `a` and `b` and from there generate the $x$ values and $y$ values.\n\nLet's see this with 5 points, the first and last being identical due to the curve:\n\n```\nr(t) = [sin(t), 2cos(t)]\nts = range(0, stop=2pi, length=5)\n```\n\nThen we can create the $5$ points easily through broadcasting:\n\n```\nvs = r.(ts)\n```\n\nThis returns a vector of points (stored as vectors). The plotting function wants two collections: the set of $x$ values for the points and the set of $y$ values. The data needs to be generated differently or reshaped. The function `xs_ys` above takes data in this style and returns the desired format, returning a tuple with the $x$ values and $y$ values pulled out:\n\n```\nxs_ys(vs)\n```\n\nTo plot this, we \"splat\" the tuple so that `plot` gets the arguments separately:\n\n```\nplot(xs_ys(vs)...)\n```\n\nThis  basic plot is lacking, of course, as there are not enough points. Using more initially is a remedy. Rather than generate the `ts` separately, `xs_ys` has a simple frontend `xs_ys(r, a, b)` which does this work itself:\n\n```\nplot(xs_ys(r, 0, 2pi, 100)...)\n```\n\n\n### Plotting a space curve in 3 dimensions\n\nA parametrically described curve in 3D is similarly created. For example, a helix is described mathematically by $r(t) = \\langle sin(t), cos(t), t \\rangle$. Here we graph two turns:\n\n```\nr(t) = [sin(t), cos(t), t]\nplot(xs_ys(r, 0, 4pi)...)\n```\n\n### Adding a vector\n\nThe tangent vector indicates the instantaneous direction one would travel were they walking along the space curve. We can add a tangent vector to the graph. The `quiver!` function would be used to add a 2D vector, but `Plots` does not currently have a `3D` analog. In addition, `quiver!` has a somewhat cumbersome calling pattern when adding just one vector. So we have the `arrow!` function defined above that uses `quiver` for 2D arrows and a simple line for 3D arrows. As a vector incorporates magnitude and direction, but not a position, `arrow!` needs both a point for the position and a vector.\n\nHere is how we can visualize the tangent vector at a few points on the helix:\n\n```\nplot(xs_ys(r, 0, 4pi)..., legend=false)\nts = range(0, 4pi, length=5)\nfor t in ts\n   arrow!(r(t), r'(t))\nend\n```\n\n\n```\nnote(\"\"\"Adding many arrows this way would be inefficient.\"\"\")\n```\n\n### Setting a viewing angle for 3D plots\n\nFor 3D plots, the viewing angle can make the difference in visualizing the key features. In `Plots`, some backends allow the viewing angle to be set with the mouse by clicking and dragging. Not all do. For such, the `camera` argument is used, as in `camera(azimuthal, elevation)` where the angles are given in degrees. If the $x$-$y$-$z$ coorinates are given, then `elevation` or *inclination*, is the angle between the $z$ axis and the $x-y$ plane (so `90` is a top view) and `azimuthal` is the angle in the $x-y$ plane from the $x$ axes.\n\n\n## Visualizing functions from $R^2 \\rightarrow R$\n\nIf a function $f: R^2 \\rightarrow R$ then a graph of $(x,y,f(x,y))$ can be represented in 3D. It will form a surface. Such graphs can be most simply made by specifying a set of $x$ values, a set of $y$ values and a function $f$, as with:\n\n```\nxs = range(-2, stop=2, length=100)\nys = range(-pi, stop=pi, length=100)\nf(x,y) = x*sin(y)\nsurface(xs, ys, f)\n```\n\nRather than pass in a function, values can be passed in. Here they are generated with a list comprehension. The `y` values are innermost to match the graphic when passing in a function object:\n\n```\nzs = [f(x,y) for y in ys, x in xs]\nsurface(xs, ys, zs)\n```\n\nRemembering if the `ys` or `xs` go first in the above can be\nhard. Alternatively, broadcasting can be used. The command `f.(xs,ys)`\nwould return a vector, as the `xs` and `ys` match in shape--they are both column vectors. But the\n*transpose* of `xs` looks like a *row* vector and `ys` looks like a\ncolumn vector, so broadcasting will create a matrix of values, as\ndesired here:\n\n```\nsurface(xs, ys, f.(xs', ys))\n```\n\n\nThis graph shows the tessalation algorithm. Here only the grid in the $x$-$y$ plane is just one cell:\n\n```\nxs = [-1,1]; ys = [-1,1]\nf(x,y) = x*y\nsurface(xs, ys, f.(xs', ys))\n```\n\nA more accurate graph, can be seen here:\n\n```\nxs = ys = range(-2,2, length=100)\nsurface(xs, ys, f)\n```\n\n\n### Contour plots\n\nReturning to the\n\n\n\nThe contour plot of $f:R^2 \\rightarrow R$ draws level curves, $f(x,y)=c$, for different values of $c$ in the $x-y$ plane.\nThey are produced in a similar manner as the surface plots:\n\n```\ncontour(xs, ys, f)\n```\n\nThe cross in the middle corresponds to $c=0$, as when $x=0$ or $y=0$ then $f(x,y)=0$.\n\nSimilarly, computed values for $f(x,y)$ can be passed in. Here we change the function:\n\n```\nf(x,y) = 2 - (x^2 + y^2)\nzs = [f(x,y) for y in ys, x in xs]\ncontour(xs, ys, zs)\n```\n\nThe chosen levels can be specified by the user through the `levels` argument, as in:\n\n```\ngr()\ncontour(xs, ys, zs, levels = [-1.0, 0.0, 1.0])\n```\n\nIf only a single level is desired, as scalar value can be specified. For example, this next graphic shows the $0$-level of the [devil]](http://www-groups.dcs.st-and.ac.uk/~history/Curves/Devils.html)'s curve.\n\n```\ngr()\na, b = -1, 2\nf(x,y) = y^4 - x^4 + a*y^2 + b*x^2\nxs = ys = range(-5, stop=5, length=100)\ncontour(xs, ys, f, levels=[0.0])\n```\n\n\nContour plots are well known from the presence of contour lines on many maps. Contour lines indicate constant elevations. A peak is characterized by a series of nested closed paths. The following graph shows this for the peak at $(x,y)=(0,0)$.\n\n```\n#plotly()\nxs = ys = range(-pi/2, stop=pi/2, length=100)\nf(x,y) = sinc(sqrt(x^2 + y^2))   # sinc(x) is sin(x)/x\ncontour(xs, ys, f)\n```\n\nContour plots can be filled with colors through the `contourf` function:\n\n```\ncontourf(xs, ys, zs)\n```\n\n\n### Combining surface plots and contour plots\n\n\nIn `PyPlot` it is possible to add a contour lines to the surface, or projected onto an axis. Here is an example:\n\n```\nimport PyPlot\nxs = ys = range(-2, stop=2, length=100)\nf(x,y) = 2 + x^2 + y^2\nzs = [f(x,y) for y in ys, x in xs]\nPyPlot.plot_surface(xs, ys, zs)\nPyPlot.contour3D(xs, ys, zs)\nax = PyPlot.gca()\nax.contour(xs, ys, zs, offset=0)\n```\n\nTo replicate something similar, though not as satisfying, in `Plots` we use the `Contour` package.\n\n```\nimport Contour\nxs = ys = range(-2, stop=2, length=100)\nf(x,y) = 2 + x^2 + y^2\nzs = [f(x,y) for y in ys, x in xs]\np = surface(xs, ys, zs, legend=false, fillalpha=0.5)\n\n## we add to the graphic p, then plot\nfor cl in levels(contours(xs, ys, zs))\n    lvl = level(cl) # the z-value of this contour level\n    for line in lines(cl)\n        _xs, _ys = coordinates(line) # coordinates of this line segment\n        _zs = 0 * _xs\n        plot!(p, _xs, _ys, lvl .+ _zs, alpha=0.5) # add on surface\n        plot!(p, _xs, _ys, _zs, alpha=0.5)        # add on x-y plane\n    end\nend\np\n```\n\nThere is no hidden line calculuation, in place we give the contour lines a transparency through the argument `alpha=0.5`.\n\n\n### Gradient and surface plots\n\nThe surface plot of $f: R^2 \\rightarrow R$ plots $(x, y, f(x,y))$ as a surface. The *gradient* of $f$ is $\\langle \\partial f/\\partial x, \\partial f/\\partial y\\rangle$. It is a two-dimensional object indicating the direction at a point $(x,y)$ where the surface has the greatest ascent. Illurating the gradient and the surface on the same plot requires embedding the 2D gradient into the 3D surface. This can be done by adding a constant $z$ value to the gradient, such as $0$.\n\n```\nf(x,y) = 2 - (x^2 + y^2)\nxs = ys = range(-2, stop=2, length=100)\nzs = [f(x,y) for y in ys, x in xs]\n\nsurface(xs, ys, zs, camera=(40, 25), legend=false)\np = [-1, 1] # in the region graphed, [-2,2] × [-2, 2]\n\nf(x) = f(x...)\nv = ForwardDiff.gradient(f, p)\n\n\n# add 0 to p and v (two styles)\npush!(p, -15)\nscatter!(xs_ys([p])..., markersize=3)\n\nv = vcat(v, 0)\narrow!(p, v)\n```\n\n\n### The tangent plane\n\nLet $z = f(x,y)$ describe a surface, and $F(x,y,z) = f(x,y) - z$. The the gradient of $F$ at a point $p$ on the surface, $\\nabla F(p)$, will be normal to the surface and for a function, $f(p) + \\nabla f \\cdot (x-p)$ describes the tangent plane. We can visualize each, as follows:\n\n```\nf(x,y) = 2 - x^2 - y^2\nF(x,y,z) = z - f(x,y)\nF(x) = F(x...)\np = [1/10, -1/10]\np1 = vcat(p, f(p...)) # note F(p1) == 0\nn = ForwardDiff.gradient(F, p1)\ntl(x) = f(p) +  ForwardDiff.gradient(f, p) ⋅ (x - p)\ntl(x,y) = tl([x,y])\n\nxs = ys = range(-2, stop=2, length=100)\nsurface(xs, ys, f)\nsurface!(xs, ys, tl)\narrow!(p1, 5n)\n```\n\nFrom some viewing angles, the normal does not look perpendicular to the tangent plane. This is a quick verification for a randomly chosen point in the $x-y$ plane:\n\n```\na, b = randn(2)\ndot(n, (p1-[a,b,tl(a,b)]))\n```\n\n\n\n\n### Parameterized surface plots\n\nAs illustrated, we can plot surfaces of the form $(x,y,f(x,y)$. However, not all surfaces are so readily described. For example, if $F(x,y,z)$ is a function from $R^3 \\rightarrow R$, then $F(x,y,z)=c$ is a surface of interest. For example, the sphere of radius one is a solution to $F(x,y,z)=1$ where $F(x,y,z) =  x^2 + y^2 + z^2$.\n\nPlotting such generally described surfaces is not so easy, but *parameterized* surfaces can be represented. For example, the sphere as a surface is not represented as a surface of a function, but can be represented in spherical coordinates as parameterized by two angles, essentially an \"azimuth\" and and \"elevation\", as used with the `camera` argument.\n\nHere we define functions that represent $(x,y,z)$ coordinates in terms of the corresponding spherical coordinates $(r, \\theta, \\phi)$.\n\n```\n# spherical: (radius r, inclination θ, azimuth φ)\nX(r,theta,phi) = r * sin(theta) * sin(phi)\nY(r,theta,phi) = r * sin(theta) * cos(phi)\nZ(r,theta,phi) = r * cos(theta)\n```\n\nWe can parameterize the sphere by plotting values for $x$, $y$, and $z$ produced by a sequence of values for $\\theta$ and $\\phi$, holding $r=1$:\n\n```\npyplot()\nthetas = range(0, stop=pi,   length=50)\nphis   = range(0, stop=pi/2, length=50)\n\nxs = [X(1, theta, phi) for theta in thetas, phi in phis] \nys = [Y(1, theta, phi) for theta in thetas, phi in phis]\nzs = [Z(1, theta, phi) for theta in thetas, phi in phis]\n\nsurface(xs, ys, zs)\n```\n\n```\nnote(\"The above may not work with all backends for `Plots`, even if those that support 3D graphics.\")\n```\n\n### Plotting  F(x,y, z) = c\n\nThere is no built in functionality in `Plots` to create surface described by $F(x,y,z) = c$. An example of how to provide some such functionality for `PyPlot` appears [here](https://stackoverflow.com/questions/4680525/plotting-implicit-equations-in-3d ). The following implements the same approach for `Plots`.\n\n\n```\ngr()\nfunction plot_implicit(F, c=0;\n                       xrng=(-5,5), yrng=xrng, zrng=xrng,\n                       nlevels=6,         # number of levels in a direction\n                       slices=Dict(:x => :blue,\n                                   :y => :red,\n                                   :z => :green), # which directions and color\n                       kwargs...          # passed to initial `plot` call\n                       )\n\n    _linspace(rng, n=150) = range(rng[1], stop=rng[2], length=n)\n\n    X1, Y1, Z1 = _linspace(xrng), _linspace(yrng), _linspace(zrng)\n\n    p = Plots.plot(;legend=false,kwargs...)\n\n    if :x ∈ keys(slices)\n        for x in _linspace(xrng, nlevels)\n            local X1 = [F(x,y,z) for y in Y1, z in Z1]\n            cnt = contours(Y1,Z1,X1, [c])\n            for line in lines(levels(cnt)[1])\n                ys, zs = coordinates(line) # coordinates of this line segment\n                plot!(p, x .+ 0 * ys, ys, zs, color=slices[:x])\n          end\n        end\n    end\n\n    if :y ∈ keys(slices)\n        for y in _linspace(yrng, nlevels)\n            local Y1 = [F(x,y,z) for x in X1, z in Z1]\n            cnt = contours(Z1,X1,Y1, [c])\n            for line in lines(levels(cnt)[1])\n                xs, zs = coordinates(line) # coordinates of this line segment\n                plot!(p, xs, y .+ 0 * xs, zs, color=slices[:y])\n            end\n        end\n    end\n\n    if :z ∈ keys(slices)\n        for z in _linspace(zrng, nlevels)\n            local Z1 = [F(x, y, z) for x in X1, y in Y1]\n            cnt = contours(X1, Y1, Z1, [c])\n            for line in lines(levels(cnt)[1])\n                xs, ys = coordinates(line) # coordinates of this line segment\n                plot!(p, xs, ys, z .+ 0 * xs, color=slices[:z])\n            end\n        end\n    end\n\n\n    p\nend\n```\n\nTo use it, we see what happens when a sphere if rendered:\n\n```\nf(x,y,z) = x^2 + y^2 + z^2 - 25\nplot_implicit(f)\n```\n\nThe graphic is a bit hard to read. Showing only 1 set of slices with more levels can lead to an improvement:\n\n```\nplot_implicit(f, nlevels=20, slices=Dict(:z=>:blue))\n```\n\nThis figure comes from a February 14, 2019 article in the [New York Times](https://www.nytimes.com/2019/02/14/science/math-algorithm-valentine.html). It shows an equation for a \"heart,\" as the graphic will illustrate:\n\n```\na,b = 1,3\nf(x,y,z) = (x^2+((1+b)*y)^2+z^2-1)^3-x^2*z^3-a*y^2*z^3\nplot_implicit(f, xrng=(-2,2),yrng=(-1,1),zrng=(-1,2),\n   nlevels=40, slices=Dict(:z=>:blue))\n```\n\n## Vector fields\n\nConsider a function $f: R^2 \\rightarrow R$. The gradient function, $\\nabla f$ is a function from $R^2$ into $R^2$. Such functions can be visualized in terms of a vector field. For each point, $p$, the  gradient (or a scaled version) is indicated by anchoring it at $p$. This is typically done for a grid of values. In the code below, we scale the size of each vector so that they stay within the grid.\n\n\n```\nusing LinearAlgebra\n\nf(x,y) = x^2 - y^2\nf(x) = f(x...)\nxs = ys = range(-2, stop=2, length=10)\n\nps = [[x,y] for x in xs, y in ys]\nvs = [ForwardDiff.gradient(f, p) for p in ps]\n\n# scale the vs\nm = maximum(norm.(vs))\nvs = 4/10/m * vs\n\nquiver(xs_ys(ps)..., quiver=xs_ys(vs))\n```\n\nWe see the use of `quiver` to add vectors in 2D. The `ps` need to be specified as `xs, ys`, hence the splatting, whereas the `quiver` *argument* expects a tuple of vectors, hence no splatting. Calling `quiver` this way, as opposed to adding individual lines through `arrow`, say, is much more efficient.\n\nThe scale used is simply computed. The points are spaced evenly with $4/10$ units between them.  The longest vector will have this length, so will not be able to leave the cell, hence the vectors won't overlap, as would happen with this example otherwise.\n\nOne last comment, in place of the comprehension to form the `vs`, broadcasting, as with `vs = ForwardDiff.gradient.(f, ps)`, could be used."
      ],
      "metadata": {}
    }
  ],
  "nbformat_minor": 2,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia",
      "version": "1.5.0"
    },
    "kernelspec": {
      "name": "julia-1.5",
      "display_name": "Julia 1.5.0",
      "language": "julia"
    }
  },
  "nbformat": 4
}
