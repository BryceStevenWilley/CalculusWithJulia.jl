{
  "cells": [
    {
      "cell_type": "markdown",
      "source": [
        "# Surface Area"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using CalculusWithJulia\nusing CalculusWithJulia.WeaveSupport\nusing Plots\nnothing"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "imgfile = \"figures/gehry-hendrix.jpg\"\ncaption = \"\"\"\n\nThe exterior of the Jimi Hendrix Museum in Seattle has the signature\nstyle of its architect Frank Gehry. The surface is comprised of\npatches. A general method to find the amount of material to cover the\nsurface - the surface area - might be to add up the area of the\npatches. However, in this section we will see for surfaces of\nrevolution, there is an easier way. (Photo credit to\n[http://firepanjewellery.com/].)\n\"\"\"\n\nImageFile(imgfile, caption)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "> The surface area generated by rotating the graph of $f(x)$ between $a$ and $b$  about the $x$-axis formed\n> is given by the integral\n>\n> $$~\n> \\int_a^b 2\\pi f(x) \\cdot \\sqrt{1 + f'(x)^2} dx.\n> ~$$\n>\n> If the curve is parameterized by $(g(t), f(t))$ between $a$ and $b$ then the surface area is\n>\n> $$~\n> \\int_a^b 2\\pi f(t) \\cdot \\sqrt{g'(t)^2 + f'(t)^2} dx.\n> ~$$\n> These formulas do not add in the surface area of either of the ends."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "## {{{surface_revolution }}}\nimgfile = \"figures/surface-revolution.png\"\ncaption = L\"\"\"\n\nVase-like figure formed by the rotation of the graph of $x = 2 + \\cos(y)$ about the $y$ axis.\n\nPhoto credit: [wikipedia](https://upload.wikimedia.org/wikipedia/commons/thumb/a/a2/Surface_of_revolution_illustration.png/188px-Surface_of_revolution_illustration.png).\n    \"\"\"\n\nImageFile(imgfile, caption)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "To see why this formula, we look at the parameterized case, the first\none being a special instance with $g(t) =t$.\n\nLet a partition of $[a,b]$ be given by $a = t_0 < t_1\n< t_2 < \\cdots < t_n =b$. This breaks the curve into a collection of\nline segments. Consider the line segment connecting $(g(t_{i-1}),\nf(t_{i-1}))$ to $(g(t_i), f(t_i))$. Rotating this around the $x$ axis\nwill generate something approximating a disc, but in reality will be\nthe frustum of a cone. What will be the surface area?\n\nConsider a right-circular cone parameterized by an angle $\\theta$ and\nthe largest radius $r$ (so that the height satisfies\n$r/h=\\tan(\\theta)$). If this cone were made of paper, cut up a side,\nand layed out flat, it would form a sector of a circle, whose area\nwould be $R\\gamma$ where $R$ is the radius of the circle (also the\nside length of our cone), and $\\gamma$ an angle that we can figure out\nfrom $r$ and $\\theta$. To do this, we note that the arc length of the\ncircle's edge is $R\\gamma$ and also the circumference of the bottom of\nthe cone so $R\\gamma = 2\\pi r$. With all this, we can solve to get $A\n= \\pi r^2/\\sin(\\theta)$. But we have a frustum of a cone with radii\n$r_0$ and $r_1$, so the surface area is a difference: $A =\n\\pi (r_1^2 - r_0^2) /\\sin(\\theta)$.\n\nRelating this to our values in terms of $f$ and $g$, we have\n$r_1=f(t_i)$, $r_0 = f(t_{i-1})$, and $\\sin(\\theta) = \\Delta f /\n\\sqrt{(\\Delta g)^2 + (\\Delta f)^2}$, where $\\Delta f = f(t_i) -\nf(t_{i-1})$ and similarly for $\\Delta g$.\n\nPutting this altogether we get that the surface area generarated by\nrotating the line segment around the $x$ axis is\n\n$$~\n\\text{sa}_i = \\pi (f(t_i)^2 - f(t_{i-1})^2) \\cdot \\sqrt{(\\Delta g)^2 + (\\Delta f)^2} / \\Delta f =\n\\pi (f(t_i) + f(t_{i-1})) \\cdot \\sqrt{(\\Delta g)^2 + (\\Delta f)^2}.\n~$$\n\n(This is $2 \\pi$ times the average radius times the slant height.)\n\nAs was done in the derivation of the formula for arc length, these\npieces are multiplied both top and bottom by $\\Delta t =\nt_{i} - t_{i-1}$. Carrying the bottom inside the square root and\nnoting that by the mean value theorem $\\Delta g/\\Delta t = g(\\xi)$\nand $\\Delta f/\\Delta t = f(\\psi)$ for some $\\xi$ and $\\psi$ in\n$[t_{i-1}, t_i]$, this becomes:\n\n$$~\n\\text{sa}_i = \\pi (f(t_i) + f(t_{i-1})) \\cdot \\sqrt{(g'(\\xi))^2 + (f'(\\psi))^2} \\cdot (t_i - t_{i-1}).\n~$$\n\nAdding these up, $\\text{sa}_1 + \\text{sa}_2 + \\cdots + \\text{sa}_n$,\nwe get a Riemann sum approximation to the integral\n\n$$~\n\\text{SA} = \\int_a^b 2\\pi f(t) \\sqrt{g'(t)^2 + f'(t)^2} dt.\n~$$\n\nIf we assume integrability of the integrand, then as our partition\nsize goes to zero, this approximate surface area converges to the\nvalue given by the limit.  (As with arc length, this needs a technical\nadjustment to the Riemann integral theorem as here we are evaluating the\nintegrand function at four points ($t_i$, $t_{i-1}$, $\\xi$ and\n$\\psi$) and not just at some $c_i$."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "## {{{approximate_surface_area}}}\npyplot()\nfig_size=(600, 400)\n\nxs,ys = range(-1, stop=1, length=50), range(-1, stop=1, length=50)\nf(x,y)= 2 - (x^2 + y^2)\n\ndr = [1/2, 3/4]\ndf = [f(dr[1],0), f(dr[2],0)]\n\nfunction sa_approx_graph(i)\n    p = plot(xs, ys, f, st=[:surface], legend=false)\n    for theta in range(0, stop=i/10*2pi, length=10*i )\n        path3d!(p,sin(theta)*dr, cos(theta)*dr, df)\n    end\n    p\nend\nn = 10\n\nanim = @animate for i=1:n\n    sa_approx_graph(i)\nend\n\nimgfile = tempname() * \".gif\"\ngif(anim, imgfile, fps = 1)\n\n\ncaption = L\"\"\"\n\nSurface of revolution of $f(x) = 2 - x^2$ about the $y$ axis. The lines segments are the images of rotating the secant line connecting $(1/2, f(1/2))$ and $(3/4, f(3/4))$. These trace out the frustum of a cone which approximates the corresponding surface area of the surface of revolution. In the limit, this approximation becomes exact and a formula for the surface area of surfaces of revolution can be used to compute the value.\n\n\"\"\"\n\nplotly()\nImageFile(imgfile, caption)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Examples\n\nLets see that the surface area of an open cone follows from this\nformula, even though we just saw how to get this value.\n\nA cone be be envisioned as rotating the function $f(x) = x\\tan(\\theta)$ between $0$ and $h$ around the $x$ axis. This integral yields the surface area:\n\n$$~\n\\int_0^h 2\\pi f(x) \\sqrt{1 + f'(x)^2}dx\n= \\int_0^h 2\\pi x \\tan(\\theta) \\sqrt{1 + \\tan(\\theta)^2}dx\n= (2\\pi\\tan(\\theta)\\sqrt{1 + \\tan(\\theta)^2} x^2/2 \\big|_0^h\n= \\pi \\tan(\\theta) \\sec(\\theta) h^2\n= \\pi r^2 / \\sin(\\theta).\n~$$\n\n(There are many ways to express this, we used $r$ and $\\theta$ to\nmatch the work above. If the cone is parameterized by a height $h$ and\nradius $r$, then the surface area of the sides is $\\pi r\\sqrt{h^2 +\nr^2}$. If the base is included, there is an additional $\\pi r^2$\nterm.)\n\n##### Example\n\nLet the graph of $f(x) = x^2$ from $x=0$ to $x=1$ be rotated around the $x$ axis. What is the resulting surface area generated?\n\n$$~\n\\text{SA} = \\int_a^b 2\\pi f(x) \\sqrt{1 + f'(x)^2}dx = \\int_0^1 2\\pi x^2 \\sqrt{1 + (2x)^2} dx.\n~$$\n\nThis integral is done by a trig substitution, but gets involved. We let `SymPy` do it:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using CalculusWithJulia  # loads `SymPy`, `QuadGK`, `ForwardDiff`\nusing Plots\n@vars x\nF = integrate(2 * PI * x^2 * sqrt(1 + (2x)^2), x)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "We show `F`, only to demonstrate that indeed the integral is a bit\ninvolved. The actual surface area follows from a *definite* integral, which we get through the fundamental theorem of calculus:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "F(1) - F(0)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Example\n\nRotate the graph of $x^x$ from $0$ to $2$ around the $x$ axis. What is the surface area generated?\n\nWe work numerically for this one, as no antiderivative is forthcoming. Recall, the accompanying `CalculusWithJulia` package defines `f'` to return the automatic derivative through the `ForwardDiff` package."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "f(x) = x^x\na, b = 0, 2\nval, _ = quadgk(x -> 2pi * f(x) * sqrt(1 + f'(x)^2), a, b)\nval"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "(The function is not defined at $x=0$ mathematically, but is on the computer to be $1$, the limiting value. Even were this not the case, the `quadgk` function doesn't evaluate the function at the points `a` and `b` that are specified.)\n\nWe compare this answer to that of the frustum of a cone with radii $1$\nand $4$, formed by rotating the line segment connecting $(0,f(0))$\nwith $(2,f(2))$. The values should be comparable. The surface area of\nthe cone part is $\\pi (r_1^2 + r_0^2) / \\sin(\\theta) = \\pi (r_1 + r_0)\n\\cdot \\sqrt{(\\Delta h)^2 + (r_1-r_0)^2}$."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "f(x) = x^x\nr0, r1 = f(0), f(2)\npi * (r1 + r0) * sqrt(2^2 + (r1-r0)^2)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Example\n\nWhat is the surface area generated by Gabriel's Horn, the solid formed by\nrotating $1/x$ for $x \\geq 1$ around the $x$ axis?\n\n$$~\n\\text{SA} = \\int_a^b 2\\pi f(x) \\sqrt{1 + f'(x)^2}dx =\n\\lim_{M \\rightarrow \\infty} \\int_1^M 2\\pi \\frac{1}{x} \\sqrt{1 + (-1/x^2)^2} dx.\n~$$\n\nWe do this with `SymPy`:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "@vars M\nex = integrate(2PI * (1/x) * sqrt(1 + (-1/x)^2), (x, 1, M))"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "The limit as $M$ gets large is of interest. The only term that might get out of hand is `asinh(M)`.  We check its limit:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "limit(asinh(M), M, oo)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "So indeed it does. There is nothing to balance this out, so the integral will be infinite, as this shows:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "limit(ex, M, oo)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "This figure would have infinite surface, were it possible to actually construct an infinitely long solid. (But it has been shown to have *finite* volume.)\n\n##### Example\n\nThe curve described parametrically by $g(t) = 2(1 + \\cos(t))\\cos(t)$\nand $f(t) = 2(1 + \\cos(t))\\sin(t)$ from $0$ to $\\pi$ is rotated about\nthe $x$ axis. Find the resulting surface area.\n\nThe graph shows half a heart, the resulting area will resemble an apple."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "g(t) = 2(1 + cos(t)) * cos(t)\nf(t) = 2(1 + cos(t)) * sin(t)\nplot(g, f, 0, 1pi)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "The integrand simplifies to $8\\sqrt{2}\\pi \\sin(t) (1 + \\cos(t))^{3/2}$. This lends itself to $u$-substitution with $u=\\cos(t)$.\n\n$$~\n\\int_0^\\pi 8\\sqrt{2}\\pi \\sin(t) (1 + \\cos(t))^{3/2}\n= 8\\sqrt{2}\\pi \\int_1^{-1} (1 + u)^{3/2} (-1) du\n= 8\\sqrt{2}\\pi (2/5) (1+u)^{5/2} \\big|_{-1}^1\n= 8\\sqrt{2}\\pi (2/5) 2^{5/2} = \\frac{2^7 \\pi}{5}.\n~$$\n\n## Theorem of Pappus\n\nThe [first](http://tinyurl.com/le3lvb9) theorem of Pappus provides a\nsimpler means to compute the surface area if the distance the centroid\nis from the axis ($\\rho$) and the arc length of the curve ($L$) are\nboth known. In that case, the surface area satisfies:\n\n$$~\n\\text{SA} = 2 \\pi \\rho L\n~$$\n\nThat is, the surface area is simply the circumference of the circle\ntraced out by the centroid of the curve times the length of the\ncurve - the distances rotated are collapsed to that of just the\ncentroid.\n\n##### Example\n\nThe surface area of of an open cone can be computed, as the arc length\nis $\\sqrt{h^2 + r^2}$ and the centroid of the line is a distance $r/2$\nfrom the axis. This gives SA$=2\\pi (r/2) \\sqrt{h^2 + r^2} = \\pi r\n\\sqrt{h^2 + r^2}$.\n\n##### Example\n\nWe can get the surface area of a torus from this formula.\n\nThe torus is found by rotating the curve $(x-b)^2 + y^2 = a^2$ about\nthe $y$ axis. The centroid is $b$, the arc length $2\\pi a$, so the\nsurface area is $2\\pi (b) (2\\pi a) = 4\\pi^2 a b$.\n\n##### Example\n\nThe surface area of sphere will be SA$=2\\pi \\rho (\\pi r) = 2 \\pi^2 r\n\\cdot \\rho$. What is $\\rho$? The centroid of an arc formula can be\nderived in a manner similar to that of the centroid of a region. The\nformulas are:\n\n$$~\n\\begin{align}\n\\text{cm}_x &= \\frac{1}{L} \\int_a^b g(t) \\sqrt{g'(t)^2 + f'(t)^2} dt\\\\\n\\text{cm}_y &= \\frac{1}{L} \\int_a^b f(t) \\sqrt{g'(t)^2 + f'(t)^2} dt.\n\\end{align}\n~$$\n\nHere, $L$ is the arc length of the curve.\n\nFor the sphere parameterized by $g(t) = r \\cos(t)$, $f(t) = r\\sin(t)$, we get that these become\n\n$$~\n\\text{cm}_x = \\frac{1}{L}\\int_0^\\pi r\\cos(t) \\sqrt{r^2(\\sin(t)^2 + \\cos(t)^2)} dt =  \\frac{1}{L}r^2 \\int_0^\\pi \\cos(t) = 0.\n~$$\n\n\n$$~\n\\text{cm}_y =  \\frac{1}{L}\\int_0^\\pi r\\sin(t) \\sqrt{r^2(\\sin(t)^2 + \\cos(t)^2)} dt =  \\frac{1}{L}r^2 \\int_0^\\pi \\sin(t) = \\frac{1}{\\pi r} r^2 \\cdot 2 = \\frac{2r}{\\pi}.\n~$$\n\nCombining this, we see that the surface area of a sphere is $2 \\pi^2 r (2r/\\pi) = 4\\pi r^2$, by Pappus' Theorem.\n\n## Questions\n\n\n##### Questions\n\n\nThe graph of $f(x) = \\sin(x)$ from $0$ to $\\pi$ is rotated around the\n$x$ axis. After a $u$-substitution, what integral would  give the surface area generated?"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "choices = [\nL\"-\\int_1^{-1} 2\\pi \\sqrt{1 + u^2} du\",\nL\"-\\int_1^{_1} 2\\pi u \\sqrt{1 + u^2} du\",\nL\"-\\int_1^{_1} 2\\pi u^2 \\sqrt{1 + u} du\"\n]\nans = 1\nradioq(choices, ans)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "Though the integral can be computed by hand, give a numeric value."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "f(x) = sin(x)\na, b = 0, pi\nval, _ = quadgk(x -> 2pi* f(x) * sqrt(1 + f'(x)^2), a, b)\nnumericq(val)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Questions\n\nThe graph of $f(x) = \\sqrt{x}$ from $0$ to $4$ is rotated around the\n$x$ axis. Numerically find the surface area generated?"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "f(x) = sqrt(x)\na, b = 0, 4\nval, _ = quadgk(x -> 2pi* f(x) * sqrt(1 + f'(x)^2), a, b)\nnumericq(val)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Questions\n\nFind the surface area generated by revolving the graph of the function\n$f(x) = x^3/9$ from $x=0$ to $x=2$ around the $x$ axis. This can be done by hand or numerically."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "f(x) = x^3/9\na, b = 0, 2\nval, _ = quadgk(x -> 2pi* f(x) * sqrt(1 + f'(x)^2), a, b)\nnumericq(val)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Questions\n\n(From Stewart.) If a loaf of bread is in the form of a sphere of radius $1$, the\namount of crust for a slice depends on the width, but not where in the\nloaf it is sliced.\n\nThat is this integral with $f(x) = \\sqrt{1 - x^2}$ and $u, u+h$ in $[-1,1]$ does not depend on $u$:\n\n$$~\nA = \\int_u^{u+h} 2\\pi f(x) \\sqrt{1 + f'(x)^2} dx.\n~$$\n\nIf we let $f(x) = y$ then $f'(x) = x/y$. With this, what does the integral above come down to after cancellations:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "choices = [\nL\"\\int_u^{u+h} 2\\pi dx\",\nL\"\\int_u^{u_h} 2\\pi y dx\",\nL\"\\int_u^{u_h} 2\\pi x dx\"\n]\nans = 1\nradioq(choices, ans)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Questions\n\nFind the surface area of the dome of sphere generated by rotating the\nthe curve generated by $g(t) = \\cos(t)$ and $f(t) = \\sin(t)$ for $t$\nin $0$ to $ \\pi/6$.\n\nNumerically find the value."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "g(t) = cos(t)\nf(t) = sin(t)\na, b = 0, pi/4\nval, _ = quadgk(t -> 2pi* f(t) * sqrt(g'(t)^2 + f'(t)^2), a, b)\nnumericq(val)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Questions\n\nThe [astroid](http://www-history.mcs.st-and.ac.uk/Curves/Astroid.html)\nis parameterized by $g(t) = a\\cos(t)^3$ and $f(t) = a \\sin(t)^3$. Let\n$a=1$ and rotate the curve from $t=0$ to $t=\\pi$ around the $x$\naxis. What is the surface area generated?"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "g(t) = cos(t^3)\nf(t) = sin(t^3)\na, b = 0, pi\nval, _ = quadgk(t -> 2pi* f(t) * sqrt(g'(t)^2 + f'(t)^2), a, b)\nnumericq(val)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "##### Questions\n\nFor the curve   parameterized by $g(t) = a\\cos(t)^5$ and $f(t) = a \\sin(t)^5$. Let\n$a=1$ and rotate the curve from $t=0$ to $t=\\pi$ around the $x$\naxis. Numerically find  the surface area generated?"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "g(t) = cos(t^5)\nf(t) = sin(t^5)\na, b = 0, pi\nval, _ = quadgk(t -> 2pi* f(t) * sqrt(g'(t)^2 + f'(t)^2), a, b)\nnumericq(val)"
      ],
      "metadata": {},
      "execution_count": null
    }
  ],
  "nbformat_minor": 2,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia",
      "version": "1.5.0"
    },
    "kernelspec": {
      "name": "julia-1.5",
      "display_name": "Julia 1.5.0",
      "language": "julia"
    }
  },
  "nbformat": 4
}
